CODESEG SEGMENT
    ASSUME CS: CODESEG
    PUBLIC DISPLAY_NUMBER, DISPLAY_TAB, DISPLAY_NEWLINE

DISPLAY_NUMBER PROC
    PUSH CX
    PUSH BX
    PUSH SI
    PUSH DI
    MOV DI, 10
    MOV SI, 0

NUMBER_CONVERSION_LOOP:
    MOV CX, 10
    CALL DIVIDE_DOUBLEWORD
    PUSH CX
    INC SI
    CMP AX, 0
    JNZ NUMBER_CONVERSION_LOOP
    CMP DX, 0
    JNZ NUMBER_CONVERSION_LOOP
    MOV BX, DI
    SUB BX, SI

DISPLAY_DIGITS_LOOP:
    POP DX
    ADD DL, '0'
    MOV AH, 02H
    INT 21H
    DEC SI
    JNZ DISPLAY_DIGITS_LOOP

DISPLAY_SPACES_LOOP:
    CMP BX, 0
    JLE DISPLAY_DONE
    MOV DL, ' '
    MOV AH, 02H
    INT 21H
    DEC BX
    JMP DISPLAY_SPACES_LOOP

DISPLAY_DONE:
    POP DI
    POP SI
    POP BX
    POP CX
    RET

DISPLAY_NUMBER ENDP

DIVIDE_DOUBLEWORD PROC
    PUSH BX
    PUSH AX
    MOV AX, DX
    MOV DX, 0
    DIV CX
    POP BX
    PUSH AX
    MOV AX, BX
    DIV CX
    MOV CX, DX
    POP DX
    POP BX
    RET

DIVIDE_DOUBLEWORD ENDP

DISPLAY_TAB PROC FAR
    PUSH DX
    PUSH AX
    MOV DL, ' '
    MOV AH, 02H
    INT 21H
    INT 21H
    INT 21H
    INT 21H
    POP AX
    POP DX
    RET

DISPLAY_TAB ENDP

DISPLAY_NEWLINE PROC FAR
    PUSH DX
    PUSH AX
    MOV DL, 13
    MOV AH, 02H
    INT 21H
    MOV DL, 10
    MOV AH, 02H
    INT 21H
    POP AX
    POP DX
    RET

DISPLAY_NEWLINE ENDP

CODESEG ENDS

END